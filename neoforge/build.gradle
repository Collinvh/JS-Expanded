plugins {
    id 'multiloader-loader'
    id 'net.neoforged.moddev'
}
repositories {
    maven {
        name = "AzureDoom Mods"
        url = "https://maven.azuredoom.com/mods"
    }
    maven { url = 'https://maven.minecraftforge.net/' }
    maven { url = "https://api.modrinth.com/maven" }
    maven {
        name = "Collinvhts Repo"
        url = "https://maven.collinvht.nl/releases"
    }
}

dependencies {
    compileOnly "org.projectlombok:lombok:1.18.30"
    annotationProcessor "org.projectlombok:lombok:1.18.30"

    compileOnly "travelers:travelerslib-neoforge-${minecraft_version}:${travelers_version}"
    implementation "jp.jurassicsaga:jurassicsaga-neoforge-${minecraft_version}:${js_version}"

    implementation "mod.azure.azurelib:azurelib-neo-${minecraft_version}:${azure_version}"
}

neoForge {
    version = neoforge_version
    accessTransformers.from('src/main/resources/META-INF/accesstransformer.cfg')

    parchment {
        minecraftVersion = parchment_minecraft
        mappingsVersion = parchment_version
    }
    runs {
        configureEach {
            systemProperty('neoforge.enabledGameTestNamespaces', mod_id)
            ideName = "NeoForge ${it.name.capitalize()} (${project.path})" // Unify the run config names with fabric
        }
        client {
            client()
        }
        data {
            data()
            programArguments.addAll '--mod', mod_id, '--all', '--output', rootProject.project(':common').file('src/generated/resources/').getAbsolutePath(),
                    '--existing', rootProject.project(':common').file('src/main/resources/').getAbsolutePath()
        }
        server {
            server()
            setGameDirectory(file('server'))
        }
    }
    mods {
        "${mod_id}" {
            sourceSet sourceSets.main
        }
    }
}

sourceSets.main.resources {
    srcDir rootProject.project(':common').file('src/generated/resources/').getAbsolutePath()
}