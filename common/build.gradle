plugins {
    id 'multiloader-common'
    id 'net.neoforged.moddev'
}
repositories {
    maven {
        name = "AzureDoom Mods"
        url = "https://maven.azuredoom.com/mods"
    }
    maven { url = 'https://maven.minecraftforge.net/' }
    maven {
        name = "Collinvhts Repo"
        url = "https://maven.collinvht.nl/releases"
    }
}

neoForge {
    neoFormVersion = neo_form_version
    // Automatically enable AccessTransformers if the file exists
    def at = rootProject.project(':neoforge').file('src/main/resources/META-INF/accesstransformer.cfg')
    if (at.exists()) {
        accessTransformers.from(at.absolutePath)
    }
    parchment {
        minecraftVersion = parchment_minecraft
        mappingsVersion = parchment_version
    }
}

dependencies {
    compileOnly group: 'org.spongepowered', name: 'mixin', version: '0.8.5'
    // fabric and neoforge both bundle mixinextras, so it is safe to use it in common
    compileOnly group: 'io.github.llamalad7', name: 'mixinextras-common', version: '0.3.5'
    annotationProcessor group: 'io.github.llamalad7', name: 'mixinextras-common', version: '0.3.5'

    compileOnly "org.projectlombok:lombok:1.18.30"
    annotationProcessor "org.projectlombok:lombok:1.18.30"

    implementation("travelers:travelerslib-common-${minecraft_version}:${travelers_version}")
    implementation "mod.azure.azurelib:azurelib-common-${minecraft_version}:${azure_version}"
}

configurations {
    commonJava {
        canBeResolved = false
        canBeConsumed = true
    }
    commonResources {
        canBeResolved = false
        canBeConsumed = true
    }
}

artifacts {
    commonJava sourceSets.main.java.sourceDirectories.singleFile
    commonResources sourceSets.main.resources.sourceDirectories.singleFile
}